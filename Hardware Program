#include <WiFi.h>
#include <HTTPClient.h>
#include <SPI.h>
#include <MFRC522.h>

#define SS_PIN  5
#define RST_PIN 21
#define GREEN_LED 2
#define RED_LED 15 
#define buzz 22

const char* ssid = "Your_SSID";
const char* password = "Your_Password";
const char* serverUrl = "http://Your_Host:3000/mark-attendance";

MFRC522 rfid(SS_PIN, RST_PIN);
WiFiClient client;
HTTPClient http;

void setup() 
{
    Serial.begin(115200);
    SPI.begin();
    rfid.PCD_Init();

    pinMode(GREEN_LED, OUTPUT);
    pinMode(RED_LED, OUTPUT);
    pinMode(buzz,OUTPUT);

    WiFi.begin(ssid, password);
    Serial.print("Connecting to WiFi...");
    while (WiFi.status() != WL_CONNECTED) 
    {
        delay(1000);
        Serial.print(".");
    }
    
    Serial.println("\nConnected!");
}

void loop() 
{
    if (!rfid.PICC_IsNewCardPresent() || !rfid.PICC_ReadCardSerial()) 
    {
        return;
    }

    String uid = "";
    for (byte i = 0; i < rfid.uid.size; i++) 
    {
        uid += String(rfid.uid.uidByte[i], HEX);
    }
    uid.toUpperCase();

    Serial.println("RFID UID: " + uid);

    if (WiFi.status() == WL_CONNECTED) 
    {
        http.begin(client, serverUrl);
        http.addHeader("Content-Type", "application/json");

        String jsonPayload = "{\"rfid_uid\": \"" + uid + "\"}";
        int httpResponseCode = http.POST(jsonPayload);

        if (httpResponseCode == 200) 
        { 
            String response = http.getString();
            Serial.println("✅ Valid: " + response);

            digitalWrite(GREEN_LED, HIGH);
            digitalWrite(buzz,HIGH);
            delay(500);
            digitalWrite(GREEN_LED, LOW);
            digitalWrite(buzz,LOW);
        } 
        else 
        { 
            Serial.println("❌ Invalid Card!");

            digitalWrite(RED_LED, HIGH);
            delay(500);
            digitalWrite(RED_LED, LOW);
        }

        http.end();
    }

    rfid.PICC_HaltA();
    rfid.PCD_StopCrypto1();
    delay(1000);
}
